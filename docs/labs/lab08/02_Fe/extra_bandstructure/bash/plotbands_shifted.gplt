set encoding utf8 # This lets us use the Gamma symbol directly

# The locations of the tics are given in the output of the bands.x calculation
#set xtics ("Γ" 0.0000, "K" 1.0607,"X" 1.4142, "Γ" 2.4142, "L" 3.2802, "X" 4.1463, "W" 4.6463, "L" 5.3534)
set xtics ('Γ' 0.0000, 'H' 1.732, 'N' 2.956, 'Γ' 3.663, 'P' 4.163, 'H|P' 5.663, 'N' 6.1639)

# set the range of the x-axis
set yrange [-10:15]

# This gives us a full vertical line
set grid xtics lt -1 lw 1.0

# We don't need a legend
unset key

# set a label and a title
set ylabel "Energy (eV)"
set title "Iron Electronic Band Structure"

# This tells gnuplot to plot all the points from this file connected with lines
# Plot the spin up as red and the spin down as blue.
plot "bands.out.up.gnu" using 1:($2-12.7648) with lines linecolor 'red', \
     "bands.out.dn.gnu" using 1:($2-12.7648) with lines linecolor 'blue'

# Add a horizontal line at the Fermi level: 6.1639 is the x limit of the Plot.
set arrow from 0.,0 to 6.1639,0 nohead front lc rgb "black"  dashtype "-"

# And if you run this script directly as an argument to gnuplot, rather than
# by loading it within gnuplot, you can uncomment the following to keep the
# plot window open until clicked. You can save to a file from here.
#pause mouse
set term pdf
set output "Iron_bands.pdf"
replot
